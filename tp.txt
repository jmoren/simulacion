OFICINA	STORAGE	10000
MOSTRADOR	STORAGE	4
; TABLES
TCLIENTE	TABLE		M1,240,30,30		;TIEMPO TOTAL EN SISTEMA
TCOLAMOST	QTABLE 	MOSTRADOR,60,30,10	;TIEMPO EN COLA EN MOSTRADOR
CANTMOST	TABLE		Q$MOSTRADOR,5,10,10	;CANTIDAD DE GENTE EN EL MOSTRADOR POR CADA ARRIBO DE PERSONA
CANTOF1	TABLE		Q$OFICINA1,5,10,10	;CANTIDAD DE GENTE EN OFICINA 1
CANTOF2	TABLE		Q$OFICINA2,5,10,10	;CANTIDAD DE GENTE EN OFICINA 2
; INITIAL
	INITIAL 	X1,4
; FUNCIONES
TIPOCLI	FUNCTION RN1,D4 
.36,1/.62,2/.84,3/1,4
TEMPCLI	FUNCTION P$TIPO,D4
1,60/2,75/3,90/4,120
		GENERATE	120,60			; LLEGAN PUERTA PRINCIPAL
		TRANSFER	,ASIGNAR
		GENERATE 	150,90			; LLEGAN POR EL LATERAL
		ADVANCE	180,90
ASIGNAR	ASSIGN	TIPO,FN$TIPOCLI
		TEST	NE 	P$TIPO,2,VOYOFIC
		TEST	NE 	P$TIPO,3,VOYOFIC
		TRANSFER	,IRMOST
VOYOFIC	TEST	E 	P$TIPO,2,IROFI2
		TRANSFER	,IROFI1
IRMOST	TABULATE 	CANTMOST
		QUEUE		MOSTRADOR
		ENTER		MOSTRADOR
		DEPART	MOSTRADOR
		ADVANCE	FN$TEMPCLI,30
		LEAVE		MOSTRADOR
		TRANSFER	,FIN
IROFI1	TABULATE 	CANTOF1
		QUEUE		OFICINA1
		SEIZE		OFICINA1
		DEPART	OFICINA1
		ADVANCE 	FN$TEMPCLI,30
		RELEASE	OFICINA1
		TRANSFER	,FINOFIC
IROFI2	TABULATE 	CANTOF2
		QUEUE		OFICINA2
		SEIZE		OFICINA2
		DEPART	OFICINA2
		ADVANCE 	FN$TEMPCLI,30
		RELEASE	OFICINA2
		TRANSFER	,FINOFIC
FINOFIC	TRANSFER	.60,IRMOST
FIN		TRANSFER	.05,CAJAS
		TRANSFER	,SALIDA
CAJAS		TRANSFER	.02,COMUN
		PRIORITY	1	
COMUN		SELECT NU 	CAJA,1,X1,,,NOHAY
		TRANSFER 	,PAGAR
NOHAY		SELECT MIN CAJA,1,X1,,Q
PAGAR		QUEUE		P$CAJA
		SEIZE		P$CAJA
		DEPART	P$CAJA
		ADVANCE	180,90
		RELEASE 	P$CAJA
		TRANSFER	,SALIDA
SALIDA	TRANSFER	.30,PRINC
		ADVANCE	180,90
PRINC		TABULATE 	TCLIENTE
		TERMINATE
* SUBSISTEMA DE TIEMPO PARA LA CANTIDAD DE CAJAS
	GENERATE	10800,,,1
	SAVEVALUE	1,8
	ADVANCE	10800
	SAVEVALUE	1,5
	ADVANCE	10800
	SAVEVALUE	1,7
	TERMINATE
* SUBSISTEMA DE 08:00HS A 18:00 HS
	GENERATE	3600
	TERMINATE	1

