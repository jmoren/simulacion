[Subsistema nuevo]
; STORAGES
OFICINA	STORAGE	10000
MOSTRADOR	STORAGE	4
; TABLES
TCLIENTE	TABLE		M1,240,30,30		;1 TIEMPO TOTAL EN SISTEMA
TCOLAMOST	QTABLE 	MOSTRADOR,1,2,30	;3 TIEMPO EN COLA EN MOSTRADOR
CANTMOST	TABLE		Q$MOSTRADOR,5,10,10	;CANTIDAD DE GENTE EN EL MOSTRADOR POR CADA ARRIBO DE PERSONA
CANTOFI	TABLE		S$OFICINA,5,5,10	;2 CANTIDAD DE GENTE EN LAS OFICINAS
; INITIAL
		INITIAL 	X1,4
; FUNCIONES
TIPOCLI	FUNCTION RN1,D4 
.36,1/.62,2/.84,3/1,4
TEMPCLI	FUNCTION P$TIPO,D4
1,60/2,75/3,90/4,120
DESTINO	FUNCTION 	P$TIPO, D4
1,IRMOST/2,IROFI/3,IROFI/4,IRMOST
DESTOFI	FUNCTION	P$TIPO,D2
2,IROFI1/3,IROFI2
; PROGRAMA
		GENERATE	120,60			; LLEGAN PUERTA PRINCIPAL
		TRANSFER	,ASIGNAR
		GENERATE 	150,90			; LLEGAN POR EL LATERAL
		ADVANCE	180,90
ASIGNAR	ASSIGN	TIPO,FN$TIPOCLI
		ASSIGN 	ROTULO,FN$DESTINO
		TRANSFER	,P$ROTULO
IRMOST	TABULATE 	CANTMOST
		QUEUE		MOSTRADOR
		ENTER		MOSTRADOR
		DEPART	MOSTRADOR
		ADVANCE	FN$TEMPCLI,30
		LEAVE		MOSTRADOR
		TRANSFER	,FIN
IROFI		TABULATE	CANTOFI
		ENTER 	OFICINA
		ASSIGN	OFINUM,FN$DESTOFI
		TRANSFER	,P$OFINUM
IROFI1	QUEUE		OFICINA1
		SEIZE		OFICINA1
		DEPART	OFICINA1
		ADVANCE 	FN$TEMPCLI,30
		RELEASE	OFICINA1
		TRANSFER	,FINOFIC
IROFI2	QUEUE		OFICINA2
		SEIZE		OFICINA2
		DEPART	OFICINA2
		ADVANCE 	FN$TEMPCLI,30
		RELEASE	OFICINA2
		TRANSFER	,FINOFIC
FINOFIC	LEAVE		OFICINA
		TRANSFER	.60,IRMOST
FIN		TRANSFER	.05,CAJAS
		TRANSFER	,SALIDA
CAJAS		TRANSFER	.02,COMUN
		PRIORITY	1	
COMUN		SELECT NU 	CAJA,1,X1,,,NOHAY
		TRANSFER 	,PAGAR
NOHAY		SELECT MIN CAJA,1,X1,,Q
PAGAR		QUEUE		P$CAJA
		SEIZE		P$CAJA
		DEPART	P$CAJA
		ADVANCE	180,90
		RELEASE 	P$CAJA
		TRANSFER	,SALIDA
SALIDA	TRANSFER	.30,PRINC
		ADVANCE	180,90
PRINC		TABULATE 	TCLIENTE
		TERMINATE
* SUBSISTEMA DE TIEMPO PARA LA CANTIDAD DE CAJAS
	GENERATE	10800,,,1
	SAVEVALUE	1,8
	ADVANCE	10800
	SAVEVALUE	1,5
	ADVANCE	10800
	SAVEVALUE	1,7
	TERMINATE
* SUBSISTEMA DE 08:00HS A 18:00 HS
	GENERATE	3600
	TERMINATE 	1
* START 10


<\F>
Nombre=
Descripcion=
Version=
Autor=
Empresa=
Opciones=
